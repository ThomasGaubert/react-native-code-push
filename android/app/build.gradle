apply plugin: "com.android.library"
apply plugin: 'jacoco'

jacoco {
    toolVersion '0.7.4.201502262128'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
        explainIssues true
        textReport true
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }

        test {
            java.srcDirs += "$projectDir/src/sharedTest/java"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/sharedTest/java"
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify'
            }
            returnDefaultValues = true
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
    }

}

dependencies {
    implementation "com.facebook.react:react-native:+"
    implementation 'com.nimbusds:nimbus-jose-jwt:5.3'
    implementation "com.google.code.gson:gson:2.8.2"
    implementation 'com.microsoft.appcenter:appcenter-crashes:1.2.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    testImplementation 'junit:junit:4.12'

    testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4-rule-agent:1.6.5'
    testImplementation 'androidx.test:runner:1.1.0'

    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestImplementation 'androidx.test:rules:1.1.0'
}

// Android Studio 2.2.0 broke a lot of scripting, could not find another way of doing this rename trick
def createDebugCoverageReportExecutionDataFile = file("${buildDir}/outputs/code-coverage/connected/coverage.ec")

task renameCreateDebugCoverageReportExecutionDataFile(dependsOn: 'createDebugCoverageReport') {
    doLast {
        def file = file(createDebugCoverageReportExecutionDataFile.getParentFile()).listFiles().first()
        file.renameTo(createDebugCoverageReportExecutionDataFile)
    }
}

task coverageReport(type: JacocoReport, dependsOn: ['renameCreateDebugCoverageReportExecutionDataFile', 'testDebugUnitTest']) {
    reports {
        xml.enabled = false
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files(["${buildDir}/jacoco/testDebugUnitTest.exec",
                           createDebugCoverageReportExecutionDataFile])
}
